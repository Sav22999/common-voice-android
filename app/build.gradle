apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId 'org.commonvoice.saverio.beta'
        minSdkVersion 23
        targetSdkVersion 30
        versionCode = 166
        versionName '2.4'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "source_store"
    productFlavors {
        GPS {
            dimension "source_store"
            buildConfigField "String", "SOURCE_STORE", "\"GPS\""
        }
        FDGH {
            dimension "source_store"
            buildConfigField "String", "SOURCE_STORE", "\"FD-GH\""
        }
        HAG {
            dimension "source_store"
            //applicationIdSuffix ".huawei"
            buildConfigField "String", "SOURCE_STORE", "\"HAG\""
        }
        AAS {
            dimension "source_store"
            buildConfigField "String", "SOURCE_STORE", "\"AAS\""
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        enable "VersionCheckerGradleLint"
    }

    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = false
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libs.kotlinStdLib
    implementation Libs.appcompat
    implementation Libs.coreKtx
    implementation Libs.material
    implementation Libs.constraintlayout
    implementation Libs.navFragmentKtx
    implementation Libs.navUiKtx
    implementation Libs.legacySupport
    testImplementation Libs.junit4
    androidTestImplementation Libs.junitExt
    androidTestImplementation Libs.espressoCore

    //Koin
    implementation Libs.koinViewModel
    //https://github.com/InsertKoinIO/koin

    //MVVM
    implementation Libs.viewModel
    implementation Libs.liveData
    implementation Libs.viewModelSavedState
    implementation Libs.commonJava8
    //https://developer.android.com/topic/libraries/architecture/viewmodel

    //WorkManager
    implementation Libs.workRuntimeKtx
    //https://developer.android.com/topic/libraries/architecture/workmanager/basics

    //App Startup
    implementation Libs.startUp

    // Debug
    implementation Libs.timber

    implementation Libs.advancedDialogs

    implementation project(":lib")

    FDGHImplementation(project(":fake-ads"))
    HAGImplementation(project(":fake-ads"))
    AASImplementation(project(":fake-ads"))
    GPSImplementation(project(":play-services-ads"))
}
